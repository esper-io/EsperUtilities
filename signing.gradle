android {
    signingConfigs {
        release {

            def signingProperties = new Properties()
            signingProperties.load(rootProject.file('signing.properties').newDataInputStream())

            def getPassword = { String propertyName, String environmentName, String prompt ->
                def getPasswordWithDialog = { ->
                    System.setProperty('java.awt.headless', 'false')
                    try {
                        return javax.swing.JOptionPane.showInputDialog(prompt)
                    } catch (java.awt.HeadlessException ignored) {
                        return null
                    }
                }
                return signingProperties.get(propertyName) ?: System.getenv(environmentName) ?:
                        System.console() != null ? System.console().readLine('\n' + prompt + ': ')
                                : getPasswordWithDialog()
            }

            storeFile rootProject.file(signingProperties.get('storeFile'))
            storePassword getPassword('storePassword', 'STORE_PASSWORD', 'Store password')
            keyAlias signingProperties.get('keyAlias')
            keyPassword getPassword('keyPassword', 'KEY_PASSWORD', 'Key password')
        }
    }
}
